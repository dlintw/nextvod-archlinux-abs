# $Id: PKGBUILD 198271 2013-10-30 13:51:13Z allan $
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Alexander Baldeck <alexander@archlinux.org>

pkgname=libxcb
pkgver=1.9.3
pkgrel=1
pkgdesc="X11 client-side library"
arch=('i686' 'x86_64' sh4)
url="http://xcb.freedesktop.org/"
depends=('xcb-proto>=1.8-2' 'libxdmcp' 'libxau')
makedepends=('pkgconfig' 'libxslt' 'python')
license=('custom')
source=($url/dist/$pkgname-$pkgver.tar.bz2)
prepare() {
  cd "$pkgname-$pkgver"
  NOCONFIGURE=1 ./autogen.sh
}

build() {
  cd "$pkgname-$pkgver"
  ./configure --prefix=/usr \
    --enable-xinput \
    --enable-xkb \
    --disable-static
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
  install -m755 -d "$pkgdir/usr/share/licenses/$pkgname"
  install -m644 COPYING "$pkgdir/usr/share/licenses/$pkgname"
}
md5sums=('1ca999ca94f760d917ef2d8466a88742')
