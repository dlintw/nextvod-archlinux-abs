# $Id: PKGBUILD 197154 2013-10-23 13:04:19Z allan $
# Maintainer: Allan McRae <allan@archlinux.org>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: John Proctor <jproctor@prium.net>

pkgname=pcre
pkgver=8.33
pkgrel=2.1
pkgdesc="A library that implements Perl 5-style regular expressions"
arch=('i686' 'x86_64' sh4)
url="http://www.pcre.org/"
license=('BSD')
depends=('gcc-libs')
source=(ftp://ftp.csx.cam.ac.uk/pub/software/programming/pcre/${pkgname}-${pkgver}.tar.bz2{,.sig})
md5sums=('21514018448ac10062ece7a668fca949'
         'SKIP')

build() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  ./configure --prefix=/usr \
    --enable-utf --enable-unicode-properties

# comment out for sh4  --enable-pcre16 --enable-pcre32 --enable-jit \

  make
}

check() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  make -j1 check
}

package() {
  cd "${srcdir}"/${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install

  install -Dm644 LICENCE "${pkgdir}"/usr/share/licenses/${pkgname}/LICENSE
}
